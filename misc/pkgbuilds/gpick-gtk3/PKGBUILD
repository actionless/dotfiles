# Maintainer: Philip Goto <philip.goto@gmail.com>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Martin Wimpress <code@flexion.org>

_commit=5e2a63b7d0e9069f1bd485d2a112c1690575e454  # last known working version, https://github.com/thezbyg/gpick/issues/182

pkgname=gpick-gtk3
pkgver=0.2.6rc1.r77.g5e2a63b
pkgrel=1
pkgdesc="Advanced color picker written in C++ using GTK+ toolkit"
arch=(i686 x86_64 armv7h aarch64)
url="http://www.gpick.org/"
license=(BSD)
depends=(boost-libs gtk3 lua53)
makedepends=(boost cmake ragel)
provides=(gpick)
conflicts=(gpick gpick-git)
source=("git+https://github.com/thezbyg/gpick.git")
sha256sums=(SKIP)

pkgver() {
    cd gpick
    git checkout "$_commit"
    git describe --long --tags | sed 's/^gpick-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    mkdir -p build
    cd build
    cmake ../gpick -DCMAKE_INSTALL_PREFIX="/usr/"
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}/" install
    install -Dm644 "../gpick/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
